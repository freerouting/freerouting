plugins {
    id 'java'
    id 'maven-publish'
    id 'net.nemerosa.versioning' version '3.1.0'
    id 'com.github.ben-manes.versions' version '0.51.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

java.toolchain {
    languageVersion = JavaLanguageVersion.of 21
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

// javadoc is way too strict for my taste.
tasks.withType(Javadoc).configureEach {
    options.addStringOption 'encoding', 'UTF-8'
    options.addStringOption 'Xdoclint:none', '-quiet'
}

repositories {
    flatDir {
        dirs 'libs'
    }

    mavenCentral()
    // Adding mirror repositories
    maven {
        name = 'MirrorRepo1'
        url = 'https://repo1.maven.org/maven2' // Replace with actual mirror URL
    }

    google()
}

dependencies {
    // https://mvnrepository.com/artifact/junit/junit
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'

    // https://mvnrepository.com/artifact/javax.help/javahelp
    //implementation group: 'javax.help', name: 'javahelp', version: '2.0.05'

    // https://mvnrepository.com/artifact/org.apache.logging.log4j
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.23.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.23.1'

    // Add SLF4J API dependency
    implementation 'org.slf4j:slf4j-api:2.0.12'
    
    // Add SLF4J binding dependency
    implementation 'org.slf4j:slf4j-log4j12:2.0.13'

    // https://github.com/google/gson
    implementation 'com.google.code.gson:gson:2.11.0'

    // https://segment.com/docs - this one doesn't work because it's missing the Java 9+ module-info.class
    //implementation 'com.segment.analytics.java:analytics:+'

    // Add Jakarta Inject API dependency
    implementation 'jakarta.inject:jakarta.inject-api:2.0.1'
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    implementation 'jakarta.validation:jakarta.validation-api:3.1.0'
    implementation 'jakarta.activation:jakarta.activation-api:2.1.3'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'


    // Add Jersey dependencies
    implementation 'org.glassfish.jersey.core:jersey-server:3.1.7-mod'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:3.1.7'
    implementation 'org.glassfish.jersey.media:jersey-media-json-binding:3.1.7'
    implementation 'org.glassfish.jersey.core:jersey-common:3.1.7-mod'
    implementation 'org.glassfish.jersey.core:jersey-client:3.1.7'
    implementation 'org.glassfish.jersey.containers:jersey-container-servlet-core:3.1.7-mod'
    implementation 'org.glassfish.jersey.containers:jersey-container-servlet:3.1.7-mod'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.5'

    // Add Jetty dependencies
    implementation 'org.eclipse.jetty:jetty-servlet:11.0.22-mod'
    implementation 'org.eclipse.jetty:jetty-server:11.0.22'
    implementation 'org.eclipse.jetty:jetty-util:12.0.13'
    implementation 'org.eclipse.jetty:jetty-security:11.0.22'

    // Add Swagger (OpenAPI) dependencies
    implementation 'io.swagger.core.v3:swagger-jaxrs2:2.2.22'
    implementation 'io.swagger.core.v3:swagger-jaxrs2-servlet-initializer-v2:2.2.22'
    implementation 'io.swagger.core.v3:swagger-core:2.2.22'
}

test {
    useJUnitPlatform()
}

ext {
    mainClassName = 'app.freerouting.Freerouting'

    def buildDateTime = new Date()
    buildDate = buildDateTime.format('yyyy-MM-dd')
    buildTime = buildDateTime.format('HH:mm:ss.SSSZ')
}

def manifestAttributes = [
        'Automatic-Module-Name' : 'app.freerouting',
        'Built-By'              : System.properties['user.name'],
        'Created-By'            : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
        'Build-Date'            : project.buildDate,
        'Build-Time'            : project.buildTime,
        'Build-Revision'        : versioning.info.commit,
        'Specification-Title'   : project.name,
        'Specification-Version' : project.version,
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.version
].asImmutable()

jar {
    manifest {
        attributes manifestAttributes
    }
}

tasks.register('executableJar', Jar) {
    archiveClassifier = 'executable'

    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)

    from configurations.runtimeClasspath.collect { zipTree(it) }

    from files(sourceSets.main.output)

    manifest {
        attributes manifestAttributes + ['Main-Class': project.mainClassName]
    }
}

apply from: 'gradle/publishing.gradle'

// write constants to code
tasks.register('writeVersionInfo') {
    doLast {
        def buildInfoCode = file("${project.buildDir}/generated-src/app/freerouting/constants/Constants.java")
        buildInfoCode.getParentFile().mkdirs()
        buildInfoCode.write(
                """
            package app.freerouting.constants;
            public final class Constants {
              public static final String FREEROUTING_VERSION = \"${publishing.versionId}\";
              public static final String FREEROUTING_BUILD_DATE = \"${project.buildDate}\";
            }
            """.stripIndent().trim()
        )
    }
}

// add the 'vmfconstants' src dir to the folders to compile (input to groovyc)
sourceSets.main.java.srcDirs += "${project.buildDir}/generated-src/"

compileJava.dependsOn += 'writeVersionInfo'


tasks.register('dist', Copy) {
    dependsOn assemble
    from 'build/libs/freerouting-executable.jar'
    into 'build/dist/'
}

tasks.register('run', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    mainClass = project.mainClassName

    // arguments to pass to the application
    //    args 'appArg1'
    // jvmArgs 'arg1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xmaxerrs" << "1000"  // Increase maximum error messages
    options.compilerArgs << "-Xmaxwarns" << "1000" // Increase maximum warning messages
}